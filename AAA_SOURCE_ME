#
# Source this file to install and build Starlab from scratch.
#
# Usage:	source AAA_SOURCE_ME  [options]
#
# Note: All command-line arguments are passed to 'make config_new'

#
# It is not foolproof, but still intended as such. If it does not work, keep 
# the install.log and study it (or email the authors). Most of the time you
# will need to edit the local/cshrc.starlab file, source it, and try the
# 'make all' again
#

set stat=0

#	make sure the script runs from the current directory
if (! -e AAA_SOURCE_ME && ! -e starlab_start.in) then
   @ stat++
   goto done
endif

rm -f install.log >& /dev/null
touch install.log


echo \(Sending output to install.log\)

if (! -e starlab_start) then
    echo Running configure
    configure >>& install.log
    if ($status) then
	@ stat++
    endif
    echo '++++++++++++++++++++++++++++++++++++++++' >>& install.log
    echo Installing configuration files
    make config_new $* >>& install.log
    if ($status) then
	@ stat++
    endif
    echo '++++++++++++++++++++++++++++++++++++++++' >>& install.log
endif

echo Initializing Starlab environment
source starlab_start >>& install.log
if ($status) then
    @ stat++
endif
echo '++++++++++++++++++++++++++++++++++++++++' >>& install.log

echo Building Starlab \(this may take a few minutes\)...
make all >>& install.log
if ($status) then
    @ stat++
endif
rehash
echo '++++++++++++++++++++++++++++++++++++++++' >>& install.log

if ($stat == 0) then
    echo -n Testing kira...
    kira --help >& /dev/null
    if ($status == 0) then
        echo 'OK :-)'
    else
        echo 'failed :-('
    endif

    echo "Done...   Run 'scripts/kira_test' to check a sample kira run output."
    echo ""
    echo "Note: Although your current shell now has the Starlab environment loaded,"
    echo "      new shells will not have Starlab pre-loaded. You would need to add"
    echo "      the following command/alias to your .cshrc (or equivalent) file:"
    echo "                   source $STARLAB_PATH/starlab_start"

else
    echo Install failed: stat = $stat
endif



done:
    if ($stat) then
       echo "### $stat errors occured during execution of the AAA_SOURCE_ME script"
    endif
