#!/usr/local/bin/tcsh -f

# Handy script to send a Starlab-format tar file (in the current
# directory) to the specified site, optionally documenting the
# transfer with hash output.  The file is sent to the starlab/incoming
# anonymous ftp directory on the target site, which must be
# properly listed locally in ~/.netrc via a line like:
#
#	default login ftp password starlab@somewhere

set find_file = 0
set tarfile = ' '
set hash = ' '
set target_dir = pub/starlab/incoming

if ($#argv < 1) then
    echo 'Usage:  send_tar  target-host  tarfile  [hash]'
    exit
else if ($#argv < 2) then
    set find_file = 1
else if ($#argv > 2) then
    set tarfile = $2
    set hash = hash
else

# Two possibilities if there are two arguments:		hostname filename
#							hostname hash
    if (X$2 == Xhash || X$2 == Xh) then
	set find_file = 1
	set hash = hash
    else
	set tarfile = $2
    endif
endif

if ($find_file == 1) then

    set tmp = (*s_tar*)	    # Note: filename format is assumed and ls not used

    if ($#tmp < 1) then
	echo No tar files found
	exit
    else
	set tarfile = $tmp[$#tmp]
    endif
endif

if (X$tarfile == X) exit

set site = $1
if (X$1 == Xkomaba || X$1 == Xtokyo) then
    set site = alexandria.c.u-tokyo.ac.jp
endif

echo Sending tar file $tarfile to $site

# Special check for IAS (different FTP structure):

if (X$site == Xftp.sns.ias.edu) then
    set target_dir = incoming/starlab
endif

ftp $site <<~
    cd $target_dir
    bin
    $hash
    put $tarfile
    quit
~
