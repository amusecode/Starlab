#!/bin/csh -f

# Make or update a shadow directory of the specified Starlab directory.
# Usage:
#	    mkshadow  source-directory  shadow-directory
#
# If source-directory is not specified, the default is starlab.
# If shadow-directory is not specified, the default is source_directory_s.
#
# Make pointers to source code (*.[hcCfF]), Makefile.am, ([A-Z]*),
# the sbin and local directories, and certain other top-level files.
# Do not copy or link the usr directory or any autoconfigured files.
#
# Updated to reflect Starlab's new configure/make structure by Steve (9/04).
# Best to run this script before Starlab has been configured or built,
# then work in the shadow directory.
#
# Note that the update option will add new links if they are needed, but
# will NOT remove links to files that no longer exist.

if ($#argv > 0 && ("x$1" == "x-h" || "x$1" == "x--help")) then
    echo Usage: mkshadow source-directory shadow-directory
    exit
endif

set src = starlab
if ($#argv > 0) set src = $1
if (! -d $src) then
    echo Directory $src does not exist
    exit
endif

set dst = {$src}_s
if ($#argv > 1) set dst = $2
set bad = 0
if (-d $dst) then
    echo Shadow directory $dst already exists.
    set bad = 1
else if (-e $dst) then
    echo Non-directory $dst already exists.
    set bad = 2
endif

set update = 0
if ($bad != 0) then
    if ($bad == 1) then
	echo -n "Delete (d) or update (u) $dst? [quit] "
    else
	echo -n "Delete (d) $dst? [quit] "
    endif
    set resp = $<
    if ("x$resp" == "xd") then
	/bin/rm -rf $dst
    else if ("x$resp" == "xu" && $bad == 1) then
	set update = 1
    else
	exit
    endif
endif

if ($update == 0) then
    echo Shadowing directory $src with $dst
else
    echo Updating shadow directory $dst \(shadowing $src\)
endif

#--------------------------------------------------------------------------

# We are ready to start linking dst to src.  Start by listing the
# directories and files to shadow.  Don't include CVS, and go to some
# trouble to avoid other local files needed by or created by the
# configure/make build process.

set curr = $PWD
cd $src

set dirs = (`find . -type d -print | egrep -v '/CVS$|/local$|/config$|\....s$|/tmp$|^./autom4|^./usr'`)

# The list of files to link excludes local and config, which always copied.

set files = ( \
  `find include src -name '*.[hcCfF]' -print -o -name '*.[ly][ly]' -print` \
  `find doc etc sbin scripts src/packages -name CVS -prune -o -type f -print` \
  `/bin/ls -d [A-Z]* *.ac install-* *.{m4,in} | egrep -v 'Makefile|CVS|HOST'` \
  `find . -name Makefile.am -print` `/bin/ls include/stamp-h.in` \
)

cd $curr

echo Found $#dirs directories and $#files files in directory $src

#--------------------------------------------------------------------------

if (! -d $dst) then
    if ($update == 1) then
	echo Can\'t find shadow directory $dst		# can't happen?
	exit
    else
	mkdir $dst
    endif
endif

echo Making shadow directories in $dst \(1 dot per directory\):
set newdir = 0

foreach dir ($dirs)
    echo -n "."
    if (! -d $dst/$dir) then
	mkdir -p $dst/$dir
	if ($update == 1) then
	    @ newdir++
	endif
    endif
end
echo ""
if ($update == 1) echo Created $newdir new directories

echo Creating links from $dst to $src \(1 dot per 10 links\):
set newlink = 0

# Don't use foreach here since the number of entries in files may be large.

@ i = 0
@ n = 0
while ($i < $#files)
    @ i++
    set file = $files[$i]
    @ n++
    if ($n == 10) then
	echo -n "."
	@ n = 0
    endif
    if (! -e $cwd/$dst/$file) then
	ln -s $cwd/$src/$file $cwd/$dst/$file >& /dev/null  # avoid errors on
							    # repeated file name
	@ newlink++
    endif
end
echo ""
if ($update == 1) echo Created $newlink new links
echo ""

# Explicitly copy the "local" and "config" directories (excluding CVS)
# from src to dst.

foreach dir (local config)
    echo Copying $dir
    set dstdir = $dst/$dir
    if (-e $dstdir && $update == 0) then
	rm -r -f $dstdir
    endif
    if (! -e $dstdir) then
	mkdir $dstdir
    endif
    foreach file (`find $src/$dir -name CVS -prune -o -type f -print`)
	cp -p $file $dstdir
    end
end

echo ""
echo The shadow directory $dst now mirrors directory $src.
echo All source and Makefiles in $dst are symbolic links into $src.
echo Run install-starlab to complete the build.
echo ""
