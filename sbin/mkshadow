#!/bin/csh -f

# Make a shadow directory of the specified Starlab directory.
# Usage:
#	    mkshadow  source-directory  shadow-directory
#
# If source-directory is not specified, the default is starlab.
# If shadow-directory is not specified, the default is source_directory_s.
#
# Make pointers to source code (*.[hcCfF]), Makefile.am, ([A-Z]*),
# the sbin and local directories, and certain other top-level files.
# Do not copy or link the usr directory or any autoconfigured files.
#
# Updated to reflect Starlab's new configure/make structure by Steve (9/04).
# Best to run this script before Starlab has been configured or built,
# then work in the shadow directory.

if ($#argv > 0 && ("x$1" == "x-h" || "x$1" == "x--help")) then
    echo Usage: mkshadow source-directory shadow-directory
    exit
endif

set src = starlab
if ($#argv > 0) set src = $1
if (! -d $src) then
    echo Directory $src does not exist
    exit
endif

set dst = {$src}_s
if ($#argv > 1) set dst = $2
set bad = 0
if (-d $dst) then
    echo Shadow directory $dst already exists.
    set bad = 1
else if (-e $dst) then
    echo Non-directory $dst already exists.
    set bad = 2
endif

if ($bad != 0) then
    echo -n "Delete $dst (y or n)? [n] "
    set resp = $<
    if ("x$resp" == "xy" || "x$resp" == "xY") then
	/bin/rm -rf $dst
    else
	exit
    endif
endif

echo Shadowing directory $src with $dst

#--------------------------------------------------------------------------

# We are ready to start linking dst to src.  Start by listing the
# directories and files to shadow.  Don't include CVS.

set curr = $PWD
cd $src

set dirs  = (`find . -type d -print | egrep -v '/CVS$|/local$|/config$'`)

# The list of files to link excludes local and config, which always copied.

set files = ( \
  `find include src -name '*.[hcCfF]' -print -o -name '*.[ly][ly]' -print` \
  `find doc etc sbin scripts src/packages -name CVS -prune -o -type f -print` \
  `/bin/ls -d [A-Z]* *.ac install* *.{m4,in} | egrep -v 'Makefile|CVS|HOST'` \
  `find . -name Makefile.am -print` `/bin/ls include/stamp-h.in` \
)

cd $curr

echo Found $#dirs directories and $#files files in directory $src

#--------------------------------------------------------------------------

mkdir $dst
echo Making shadow directories in $dst \(1 dot per directory\):
foreach dir ($dirs)
    echo -n "."
    mkdir -p $dst/$dir
end
echo ""

echo Creating links from $dst to $src \(1 dot per 10 links\):

# Don't use foreach here since the number of entries in files may be large.

@ i = 0
@ n = 0
while ($i < $#files)
    @ i++
    set file = $files[$i]
    @ n++
    if ($n == 10) then
	echo -n "."
	@ n = 0
    endif
    ln -s $cwd/$src/$file $cwd/$dst/$file >& /dev/null	# avoid errors on
							# repeated file name
end
echo ""
echo ""

# Explicitly copy the "local" and "config" directories (excluding CVS)
# from src to dst.

foreach dir (local config)
    echo Copying $dir
    set dstdir = $dst/$dir
    if (-e $dstdir) then
	rm -r -f $dstdir
    endif
    mkdir $dstdir
    foreach file (`find $src/$dir -name CVS -prune -o -type f -print`)
	cp -p $file $dstdir
    end
end

echo ""
echo The shadow directory $dst now mirrors directory $src.
echo All source and Makefiles in $dst are symbolic links into $src.
echo Run install-starlab to complete the build.
echo ""
