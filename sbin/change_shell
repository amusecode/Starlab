# csh -f
# DON'T change the "csh" above!
# Ideally, this should become a Bourne(again) shell script.

# Change the shell in specified files.

if ($#argv < 3) then
    echo At least three arguments requried.
    exit
endif

set tmp = ($argv[3*])
if ($#tmp < 10) then
    echo Changing shell $1 to $2 in $argv[3*]
else
    echo Changing shell $1 to $2...
endif

set save_dir = .save
if (! -d $save_dir) then
    echo Creating save directory $save_dir
    mkdir $save_dir
endif

foreach file ($argv[3*])
    set tmp = (`head -1 $file | grep $1`)
    if ($#tmp > 0) then
	mv $file $save_dir/$file
	sed < $save_dir/$file s%$1%$2% > $file
	chmod a+x $file
    endif
end
