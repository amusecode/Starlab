#!/bin/csh -f

# Set the prefix, or use a default (option x or default).  Note that
# STARLAB_PATH must be set for the default option to work.

set flag = "default"
if ($#argv > 0) set flag = $1

if (x$flag == xx || x$flag == xdefault) then
    if ($?STARLAB_PATH == 0) then
	echo STARLAB_PATH not set
	exit
    endif
    set p = $STARLAB_PATH/usr
else
    set p = $flag
endif

set pre = "--prefix=$p"

# We will update the search path after each tool build, since
# automake and libtool installation may require the installed
# version of autoconf.  First add the target bin directory to
# the search path.

set path = ($p/bin $path)

echo package prefix flag is \"$pre\"

# (Re)build the autotools (isn't standardization wonderful?):

./clean-autotools

# See which tool packagesexist.

set tools = (`find -name autoconf\*.tar.gz \
		-o -name automake\*.tar.gz \
		-o -name libtool\*.tar.gz  | sed "s/.tar.gz//"`)

foreach dir ($tools)

    echo unpacking $dir
    gunzip -c $dir.tar.gz | tar xf -

    if (-d $dir) then

	echo installing $dir
	set log = $dir.install.log
	echo installation log is $log

	chdir $dir
	./configure $pre			>&! ../$log
	make					>>& ../$log
	make install				>>& ../$log
	chdir ..

	rehash
    endif

end
