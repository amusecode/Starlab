#!/bin/tcsh -f

# Set the prefix, or use a default.  Note that STARLAB_PATH must be set.

set pre = ""
if ($#argv > 0) then
    if (x$1 == xx) then
	set p = $STARLAB_PATH/usr
    else
	set p = $1
    endif

    set pre = "--prefix=$p"

    # We will update the search hash after each tool, since automake
    # installation may require the installed autoconf.  First add the
    # target bin directory to the search path.

    set path = ($p/bin $path)
endif

echo Package prefix flag is \"$pre\"

# Build the autotools (isn't standardization wonderful?):

foreach tool (autoconf automake libtool)

    echo Unpacking $tool
    tar xzf $tool*.tar.gz

    echo Installing $tool
    set dir = $tool*[0-9]
    set log = $dir.install.log
    echo Installation log is $log

    cd $dir
    ./configure $pre			>&! ../$log
    make				>>& ../$log
    make install			>>& ../$log
    cd ..

    rehash
end
