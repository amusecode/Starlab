#!/bin/tcsh -f

# Set the prefix, or use a default (option x or default).  Note that
# STARLAB_PATH must be set for the default option to work.

set pre = ""
if ($#argv > 0) then
    if (x$1 == xx || x$1 == xdefault) then
	if ($?STARLAB_PATH == 0) then
	    echo STARLAB_PATH not set
	    exit
	endif
	set p = $STARLAB_PATH/usr
    else
	set p = $1
    endif

    set pre = "--prefix=$p"

    # We will update the search path after each tool build, since
    # automake and libtool installation may require the installed
    # version of autoconf.  First add the target bin directory to
    # the search path.

    set path = ($p/bin $path)
endif

echo Package prefix flag is \"$pre\"

# Build the autotools (isn't standardization wonderful?):

foreach tool (autoconf automake libtool)

    echo Unpacking $tool
    tar xzf $tool*.tar.gz

    set dir = $tool*[0-9]

    if (-d $dir) then

	echo Installing $tool
	set log = $dir.install.log
	echo Installation log is $log

	chdir $dir
	./configure $pre			>&! ../$log
	make					>>& ../$log
	make install				>>& ../$log
	chdir ..

	rehash
    endif

end
